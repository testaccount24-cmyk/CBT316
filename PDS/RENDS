*
* THE IDEA OF THIS PROGRAM IS TO RENAME THE DATASET MENTIONED
* IN THE PARM FIELD, TO THE SAME NAME, BUT WITH .NEW APPENDED
* TO THE END OF THE NAME.
*
* THIS SAME ACTION CAN BE DONE WITH THE TSO RENAME COMMAND, BUT
* THE CODING IN THIS PROGRAM ILLUSTRATES THE POWER OF THE LOCATE
* AND RENAME MACROS IN AN ASSEMBLER PROGRAM, AND HOW TO FLAG
* ERRORS IF SOMETHING WENT WRONG.  SO IT IS GOOD CODE FOR LEARNING.
*
* SAMPLE JCL TO RUN:
*
* // JOB CARD
* //RENDS  EXEC PGM=RENDS,PARM='DATASET.TO.RENAME'
* //STEPLIB DD DISP=SHR,DSN=YOUR.STEPLIB.IF.NEEDED
* //SYSPRINT DD SYSOUT=*
*
* WRITTEN SEPT 27 2018
* SEE WHAT LOCATE AND RENAME MACROS GENERATE.
* RENAMES THE DATA SET SPECIFIED IN THE PARM
* TO THE SAME NAME WITH .NEW ADDED TO IT.
* NOTE: IF VOLUME IS SMS-MANAGED
*  THE RENAME MACRO WILL RECATALOG THE DATA SET.
*  OTHERWISE THIS PROGRAM WILL CATALOG THE NEW NAME
*  AND UNCATALOG THE OLD NAME,
* UPDATED OCT 4 2018 TO ADD SYSPRINT WITH HARD-CODED RECFM AND LRECL
* UPDATED OCT 6 2018 TO USE DCB OPEN EXIT TO SET SYSPRINT RECFM LRECL
*  IF NOT ALREADY SET, SO EXISTING ATTRIBUTES WILL BE USED IF PRESENT.
* UPDATED OCT 7 2018 TO PUT RELEVANT DSNAME INTO EACH MESSAGE
*
RENDS    CSECT
         USING *,10
ENTRY0   EQU   *
         SAVE  (14,12),,*
         LR    R10,R15
         LR    R2,R1
         LA    R15,SAVE
         ST    R13,4(,R15)         OLD ADDRESS IN NEW SAVE AREA
         ST    R15,8(,R13)         NEW ADDRESS IN OLD SAVE AREA
         LR    R13,R15
         OPEN  (PRTDCB,OUTPUT)
         TM    PRTDCB+OFLGS,X'10'
         BZ    EXIT                EXIT IF OPEN FAILED
         L     R3,0(,R2)           POINT TO PARM
         LH    R4,0(,R3)           GET LENGTH OF PARM
         LTR   R4,R4               IF NO PARM
         BZ    ERRP1                  EXIT
         CH    R4,=H'40'           IF NAME TOO LONG, NEED ROOM FOR .NEW
         BH    ERRP2                  EXIT
         BCTR  R4,0                LENGTH MINUS 1 FOR EX
         EX    R4,MOVE             COPY PARM TO OLD
         LA    R4,1(,R4)           RESTORE LENGTH
*
*        MAKE NEW NAME FROM OLD NAME, AND SAVE BOTH OF THEM.
*
         MVC   NEW,OLD             COPY OLD TO NEW (WORK NAME)
         MVC   MSGDSN,OLD          START CREATING SAVED NEW NAME
         MVC   MSGOLDN,OLD         SAVE OLD NAME WITHOUT MODIFYING IT
         LA    R5,NEW(R4)          POINT PAST END OF NEW NAME
         LA    R6,MSGDSN(R4)       FINISH SAVED NEW NAME
         MVC   0(4,R5),=C'.NEW'    APPEND .NEW IN NEW NAME
         MVC   0(4,R6),=C'.NEW'    SAME FOR SAVED NEW NAME
*
         LOCATE LOCNCAM
         LTR   R15,R15
         BZ    ERRL1               EXIT IF NEWNAME ALREADY IN CATALOG
         LOCATE LOCOCAM
         LTR   R15,R15
         BNZ   ERRL2               EXIT IF OLDNAME NOT IN CATALOG
         CLI   VOLS+1,1            CHECK NUMBER OF VOLUMES
         BNE   ERRL3               EXIT IF MORE THAN ONE VOLUME
         OBTAIN OBTCAM
         LTR   R15,R15
         BNZ   ERRO1               EXIT IF OLDNAME NOT IN VTOC
         SPACE
         SR    R0,R0
         RENAME RENCAM
         LTR   R15,R15
         BNZ   ERRR1               EXIT IF RENAME FAILED
         TM    F1DSCB+34,X'80'     DS1SMSFG,DS1SMSDS - SMS-MANAGED?
         BO    OKSMS               YES, NO NEED TO CHANGE CATALOG
         CATALOG CATCAM
         LTR   R15,R15
         BNZ   ERRC1               EXIT IF CATALOG NEWNAME FAILED
         CATALOG UNCCAM
         LTR   R15,R15
         BNZ   ERRU1               EXIT IF UNCATALOG OLDNAME FAILED
OKSMS    MVI   RETCODE+3,0
         LA    R1,MSGOK
         MVC   MSGOKD,MSGDSN
         B     EXITP
ERRP1    LA    R1,MSGP1
         B     EXITP
ERRP2    LA    R1,MSGP2
         MVC   MSGP2D,MSGOLDN
         B     EXITP
ERRL1    LA    R1,MSGL1
         MVC   MSGL1D,MSGDSN
         B     EXITP
ERRL2    LA    R1,MSGL2
         MVC   MSGL2D,MSGOLDN
         B     EXITP
ERRL3    LA    R1,MSGL3
         MVC   MSGL3D,MSGOLDN
         B     EXITP
ERRO1    LA    R1,MSGO1
         MVC   MSGO1D,MSGOLDN
         B     EXITP
ERRR1    LA    R1,MSGR1
         MVC   MSGR1D,MSGOLDN
         B     EXITP
ERRC1    LA    R1,MSGC1
         MVC   MSGC1D,MSGDSN
         B     EXITP
ERRU1    LA    R1,MSGU1
         MVC   MSGU1D,MSGOLDN
EXITP    MVI   MSG,C' '
         MVC   MSG+1(L'MSG-1),MSG
         LH    R14,0(,R1)          GET LENGTH-1 OF MESSAGE
         LA    R1,2(,R1)           POINT TO MESSAGE
         EX    R14,MOVM            COPY MESSAGE TO MSG+1
         PUT   PRTDCB,MSG
         CLOSE PRTDCB
         FREEPOOL PRTDCB
EXIT     L     R15,RETCODE
         L     R13,4(,R13)
         L     R14,12(,R13)
         RETURN (0,12)
         SPACE
MOVE     MVC   OLD(0),2(R3)        COPY PARM
MOVM     MVC   MSG+1(0),0(R1)      COPY MESSAGE
         SPACE
PRTOPEN  CLI   RECFM(R1),0         IF NO RECFM
         BNE   *+8                    THEN
         MVI   RECFM(R1),X'94'        SET RECFM=FBA
         CLC   LRECL(2,R1),=H'0'   IF NO LRECL
         BNE   *+10                   THEN
         MVC   LRECL(2,R1),=H'121'    SET LRECL=121
         CLC   BLKSI(2,R1),=H'0'   IF NO BLKSI
         BNE   *+10                   THEN
         MVC   BLKSI(2,R1),LRECL(R1)  SET BLKSI=LRECL
         BR    R14
         SPACE
         LTORG
         DC    0D'0'
SAVE     DC    18F'0'
RETCODE  DC    F'12'
LOCNCAM  CAMLST NAME,NEW,,VOLS
LOCOCAM  CAMLST NAME,OLD,,VOLS
OBTCAM   CAMLST SEARCH,OLD,VOLS+6,F1DSCB (CAN ADD EADSCB=OK)
RENCAM   CAMLST RENAME,OLD,NEW,VOLS
CATCAM   CAMLST CATBX,NEW,,VOLS
UNCCAM   CAMLST UCATDX,OLD
OLD      DC    CL44' '
NEW      DC    CL44' '
VOLS     DC    0D'0',265X'00' 2-BYTE VOL COUNT THEN 12 BYTES EACH VOL
F1DSCB   DC    0D'0',140X'00' 96-BYTE DSCB BUT 140 BYTES REQUIRED
         PRINT NOGEN
PRTDCB   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,EXLST=PRTEXL
PRTEXL   DC    0F'0',X'85',AL3(PRTOPEN)
MSG      DC    CL133' '
MSGP1    DC    AL2(MSGP1L),C'MISSING PARM - NEEDS DSN TO BE RENAMED'
MSGP1L   EQU   *-MSGP1-3
MSGP2    DC    AL2(MSGP2L),C'DSN IN PARM IS TOO LONG'
         DC    C'  DSN = '
MSGP2D   DC    CL44' '
MSGP2L   EQU   *-MSGP2-3
MSGL1    DC    AL2(MSGL1L),C'NEWNAME ALREADY IN CATALOG'
         DC    C'  DSN = '
MSGL1D   DC    CL44' '
MSGL1L   EQU   *-MSGL1-3
MSGL2    DC    AL2(MSGL2L),C'OLDNAME NOT IN CATALOG'
         DC    C'  DSN = '
MSGL2D   DC    CL44' '
MSGL2L   EQU   *-MSGL2-3
MSGL3    DC    AL2(MSGL3L),C'OLDNAME IS MULTI-VOLUME'
         DC    C'  DSN = '
MSGL3D   DC    CL44' '
MSGL3L   EQU   *-MSGL3-3
MSGO1    DC    AL2(MSGO1L),C'OLDNAME NOT IN VTOC'
         DC    C'  DSN = '
MSGO1D   DC    CL44' '
MSGO1L   EQU   *-MSGO1-3
MSGR1    DC    AL2(MSGR1L),C'RENAME FAILED'
         DC    C'  DSN = '
MSGR1D   DC    CL44' '
MSGR1L   EQU   *-MSGR1-3
MSGC1    DC    AL2(MSGC1L),C'UNABLE TO CATALOG NEWNAME'
         DC    C'  DSN = '
MSGC1D   DC    CL44' '
MSGC1L   EQU   *-MSGC1-3
MSGU1    DC    AL2(MSGU1L),C'UNABLE TO UNCATALOG OLDNAME'
         DC    C'  DSN = '
MSGU1D   DC    CL44' '
MSGU1L   EQU   *-MSGU1-3
MSGOK    DC    AL2(MSGOKL),C'RENAME WAS SUCCESSFUL'
         DC    C'  DSN = '
MSGOKD   DC    CL44' '
MSGOKL   EQU   *-MSGOK-3
MSGOLDN  DC    CL44' '
MSGDSN   DC    CL44' '
         DC    0D'0'
RECFM    EQU   36                  DCBRECFM OFFSET
OFLGS    EQU   48                  DCBOFLGS OFFSET
BLKSI    EQU   62                  DCBBLKSI OFFSET
LRECL    EQU   82                  DCBLRECL OFFSET
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   ENTRY0
